import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id "org.openapi.generator" version "6.6.0"
}

group = 'thi.cnd.authservice'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':application')
    implementation project(':domain')

    implementation 'org.zalando:problem-spring-web-starter:0.29.1'
    implementation 'org.zalando:jackson-datatype-problem:0.27.1'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java.srcDir("$projectDir/build/generate-resources/src/main/java")
    }
}

openApiValidate {
    inputSpec.set("$projectDir/src/main/resources/openapi-as-rest-spec.yaml")
    inputSpec.set("$projectDir/src/main/resources/openapi-us-event-spec.yaml")
}

task generateAuthRestApi(type: GenerateTask) {
    inputSpec.set("$projectDir/src/main/resources/openapi-as-rest-spec.yaml")
    outputDir = "$projectDir/build/generate-resources"
    generatorName = "spring"
    library = "spring-boot"
    modelNameSuffix = "DTO"
    validateSpec = true
    modelPackage = "thi.cnd.authservice.api.generated.model"
    apiPackage = "thi.cnd.authservice.api.generated"
    typeMappings = [
            "OffsetDateTime": "Instant"
    ]
    importMappings = [
            "java.time.OffsetDateTime": "java.time.Instant"
    ]
    additionalProperties = [
            "useSpringBoot3"      : true,
            "useSpringController" : false,
            "useTags"             : true,
            "useResponseEntity"   : true,
            "useOptional"         : true,
            "openApiNullable"     : false,
            "interfaceOnly"       : true,
            "skipDefaultInterface": true
    ]
}

task generateKafkaEvents(type: GenerateTask) {
    inputSpec.set("$projectDir/src/main/resources/openapi-us-event-spec.yaml")
    outputDir = "$projectDir/build/generate-resources"
    generatorName = "java"
    modelNameSuffix = "DTO"
    validateSpec = true
    library = "webclient"
    modelPackage = "thi.cnd.authservice.events.generated.model"
    apiPackage = "thi.cnd.authservice.events.generated"
    additionalProperties = [
            "useOptional"         : true,
            "openApiNullable"     : false,
            "interfaceOnly"       : true,
            "skipDefaultInterface": true,
    ]
}

compileJava.dependsOn generateAuthRestApi, generateKafkaEvents
