plugins {
    id 'java'
    id "org.openapi.generator" version "7.0.0"
}

group = 'thi.cnd.userservice'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':core')
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs += "build/generate-resources/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate

openApiValidate {
    inputSpec.set("$projectDir/src/main/resources/user-domain-events-spec.yaml")
}


openApiGenerate {
    inputSpec.set("$projectDir/src/main/resources/user-domain-events-spec.yaml")
    outputDir = "$projectDir/build/generate-resources"
    generatorName = "spring"
    library = "spring-boot"
    modelNameSuffix = "DTO"
    modelPackage = "thi.cnd.userservice.api.generated.model"
    apiPackage = "thi.cnd.userservice.api.generated"
    typeMappings = [
            "OffsetDateTime": "Instant"
    ]
    importMappings = [
            "java.time.OffsetDateTime": "java.time.Instant"
    ]
    additionalProperties = [
            "useSpringBoot3"      : true,
            "useSpringController" : false,
            "useTags"             : true,
            "useResponseEntity"   : false,
            "useOptional"         : true,
            "openApiNullable"     : false,
            "interfaceOnly"       : true,
            "skipDefaultInterface": true
    ]
}
